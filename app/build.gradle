// Main build script for app
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build production APK
//        gradle wrapper                  Create Gradle Wrapper


final VERSION_MAJOR = 3 // Max 2 digits
final VERSION_MINOR = 0 // Max 2 digits
final VERSION_PATCH = 0 // Max 2 digits
final VERSION_BUILD = 0 // Max 3 digits


apply plugin: 'com.android.application'


dependencies
{
	compile 'com.android.support:support-v4:25.1.1'
	compile 'com.android.support:appcompat-v7:25.1.1'
	compile "com.google.android.gms:play-services-base:10.2.0"
	compile "com.google.android.gms:play-services-analytics:10.2.0"
	compile "com.google.android.gms:play-services-ads:10.2.0"
	compile "com.google.android.gms:play-services-gcm:10.2.0"
	compile "com.google.android.gms:play-services-location:10.2.0"
	compile 'com.onesignal:OneSignal:3.+@aar'
	compile 'commons-io:commons-io:2.4'
}


android
{
	compileSdkVersion 25
	buildToolsVersion "25.0.2"

	defaultConfig
	{
		multiDexEnabled true
		minSdkVersion 19
		targetSdkVersion 25
		versionCode VERSION_MAJOR*10000000 + VERSION_MINOR*100000 + VERSION_PATCH*1000 + VERSION_BUILD
		versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"

		manifestPlaceholders = [manifestApplicationId: "${applicationId}",
								onesignal_app_id: "8021aab4-ced1-4ee1-813f-4805dbe11978",
								// Project number pulled from dashboard, local value is ignored.
								onesignal_google_project_number: "124618051580"]
	}

	lintOptions {
		checkReleaseBuilds false
	}

	signingConfigs
	{
		release
		{
			// Obtain Password and Alias
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes
	{
		debug
		{
			versionNameSuffix "-debug"
		}

		release
		{
			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	applicationVariants.all
	{
		variant ->
			if(variant.buildType.name.equals("release"))
			{
				variant.outputs.each
				{
					output ->
						def outputFile = output.outputFile
						def date = new Date()
						if(outputFile!=null && outputFile.name.endsWith('.apk'))
						{
							def fileName = outputFile.name.replace(
									"app",
									"WebApp-" + defaultConfig.versionName + "-" + defaultConfig.versionCode)
							output.outputFile = new File((String) outputFile.parent, (String) fileName)
						}
				}
			}
	}
}


task askForPasswords <<
{
	def storePass
	def keyAlias
	def keyPass

	def keystorePropertiesFile = new File(project.property("keystore.properties"))

	if(project.hasProperty("keystore.properties") && keystorePropertiesFile.exists())
	{
		println "Loading keystore passwords from property file..."
		Properties properties = new Properties()
		properties.load(new FileInputStream(keystorePropertiesFile))
		storePass = properties['keystore.store.password']
		keyAlias  = properties['keystore.key.alias']
		keyPass  = properties['keystore.key.password']
	}
	else
	{
		println "Getting keystore passwords..."
		storePass = new String(System.console().readPassword("\nStore password: "))
		keyAlias  = new String(System.console().readLine("Key alias: "))
		keyPass  = new String(System.console().readPassword("Key password: "))
	}

	android.signingConfigs.release.storePassword = storePass
	android.signingConfigs.release.keyAlias = keyAlias
	android.signingConfigs.release.keyPassword = keyPass
}


tasks.whenTaskAdded
{
	theTask ->
	if(theTask.name.equals("packageRelease"))
	{
		theTask.dependsOn "askForPasswords"
	}
}
